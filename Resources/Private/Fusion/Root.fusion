namespace: F=Neos.Fusion
namespace: CC=ObisConcept.CookieConsent

include: Settings.fusion

prototype(Page) {
  head {
    cookieConsent = F:Template {
      templatePath = 'resource://ObisConcept.CookieConsent/Private/Templates/Fusion/Header.html'

      scriptTag = F:Tag {
        tagName = 'script'
        attributes {
          type = 'text/javascript'
          src = F:ResourceUri {
            path = 'JavaScript/cookieconsent.min.js'
            package = 'ObisConcept.CookieConsent'
          }
        }

        @if.includeJavaScript = ${Configuration.setting('ObisConcept.CookieConsent.includeJavaScript')}
      }

      styleTag = F:Tag {
        tagName = 'link'
        attributes {
          rel = 'stylesheet'
          type = 'text/css'
          href = F:ResourceUri {
            path = 'Stylesheets/cookieconsent.min.css'
            package = 'ObisConcept.CookieConsent'
          }
        }

        @if.includeCss = ${Configuration.setting('ObisConcept.CookieConsent.includeCss')}
      }

      settings = CC:Settings {
        @process.json = ${Json.stringify(value)}
      }

      @if {
        enabled = ${Configuration.setting('ObisConcept.CookieConsent.enable') == true}
        inFrontend = ${site.context.inBackend == false}
      }
    }
  }
}
